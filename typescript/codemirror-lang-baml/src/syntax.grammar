@top Baml { Decl* }

Decl { ClassDecl | EnumDecl | FunctionDecl | ClientDecl }

ClassDecl { "class" IdentifierDecl "{" (BlockAttribute | ClassField)* "}" }

ClassField {
  IdentifierDecl TypeExpr FieldAttribute*
}
TypeExpr { ComplexTypeExpr | UnionTypeExpr }
ComplexTypeExpr {
  "(" UnionTypeExpr ")" |
  SimpleTypeExpr
}
ListTypeExpr { ComplexTypeExpr"[]" }
OptionalTypeExpr { ComplexTypeExpr"?" }
SimpleTypeExpr { ListTypeExpr | OptionalTypeExpr | IdentifierDecl }
UnionTypeExpr { TypeExpr!TypeUnion"|"TypeExpr }

@precedence {
  TypeUnion @left
}

EnumDecl { "enum" IdentifierDecl "{" (BlockAttribute | EnumValueDecl)* "}" }
EnumValueDecl { IdentifierDecl FieldAttribute* }

FieldAttribute {
  "@" IdentifierDecl AttributeValue
}

BlockAttribute {
  "@@" IdentifierDecl AttributeValue
}

FunctionDecl {
  "function" IdentifierDecl "(" FunctionArgs ")" "->" TypeExpr
  "{" TupleValue* "}"
}
FunctionArgs { (FunctionArg ",")* FunctionArg? }
FunctionArg {
  IdentifierDecl ":" TypeExpr
}
TupleValue { IdentifierDecl ValueExpr }

ValueExpr { LiteralDecl | PromptExpr | "{" TupleValue* "}" }

ClientDecl {
  "client<llm>" IdentifierDecl "{" TupleValue* "}"
}

@tokens {

  @precedence {
    QuotedString, UnquotedAttributeValue
  }

  IdentifierDecl { $[A-Za-z0-9-_]+ }

  LiteralDecl {
    '"' char* '"' | // strings
    ![#@{}()[\]<>,' \n\r\t/]+ // unquoted strings
    // floats not yet supported
  }

  QuotedString { '"' char* '"' }
  UnquotedAttributeValue { ![#@{}()[\]<>,'\n\r\t/]+ }

  promptChar { whitespace | char }

  // match all unicode characters except \u0022 " and \u005c \
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }
  whitespace { $[ \n\r\t] }
  
  TrailingComment { "//"$[^\\n]* }
  MultilineComment { "{//"(char|whitespace)*"//}" }

  "{" "}" "[" "]"
}

@skip {
  whitespace | MultilineComment | TrailingComment 
}

@skip {} {
  PromptExpr { '#"' (promptChar)* '"#'}
  AttributeValue { '(' (QuotedString | UnquotedAttributeValue)? ')' }
}

@detectDelim