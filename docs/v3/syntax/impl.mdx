---
title: "impl"
---

An **impl** is an implementation of a `function` using an AI model (LLM or otherwise).

## Syntax

```rust
impl<IMPL_TYPE, FUNCTION_NAME> Name {
    client SomeClient
}
```

- `IMPL_TYPE`: What type of AI model will be used. Currently must be `llm`
- `FUNCTION_NAME`: The name of the function this is an implementation of
- `Name`: The name of the impl (can be any [a-zA-Z], numbers or `_`)

## Properties

Depending on the `IMPL_TYPE`, additional properties may exist.

| Property  | Type                                         | Description                                                                                                                                                                                 | Required |
| --------- | -------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- | -------- |
| `client`  | string                                       | The name of the client this impl will use. The type of the client must match the type of the impl (both llm). See [Client](/v3/syntax/client)                                               | YES      |
| `adapter` | See [Adapters](/v3/syntax/function-adapters) | Modify a type temporarily for the use in the impl without exposing it to the buisness logic. e.g. force an llm to extract extra fields not specified earlier. **This is an advanced tool!** | NO       |

### impl types

#### ✅ llm

`llm` models are those which take a prompt (string) and return a string. For example, GPT-3, GPT-J, etc.
They may also return some other metadata (e.g. logprobs, stop_reason, etc.)

| Property | Type   | Description                                                                                                                                           | Required |
| -------- | ------ | ----------------------------------------------------------------------------------------------------------------------------------------------------- | -------- |
| `prompt` | string | The string passed into the LLM. You can compose this this variables and other template types. See [Prompt Engineering](/v3/syntax/prompt-engineering) | YES      |

#### ❌ probability

Not supported yet.

`probability` models are those which take in any type, and return `float[]` of a fixed length. For example, BERT, etc.And yes we know technically LLMs do this under the hood, but most practical LLMs outputs are synthesized into a string.

## Examples

### Sentiment Analysis

<Steps>
<Step title="Defining in baml">

<Info>
`{#input}` and `{#print_enum(Sentiment)}` are template variables injected into the prompt. See [Prompt Engineering](/v3/syntax/prompt-engineering) for more details.
</Info>

```rust
// In baml_src/my_file.baml
impl<llm, GetSentiment> my_version {
    client GPT35Client
    prompt #"
        Given a sentence, return the sentiment.
        ---
        {#input}
        ---

        {#print_enum(Sentiment)}

        Sentiment:
    "#

}

// Define the function
function GetSentiment {
    input string
    output Sentiment
}

// Define the enum for sentiment
enum Sentiment {
    Positive
    Negative
    Neutral
}

// Use openai's GPT-3.5 turbo model
client<llm> GPT35Client {
    provider baml-openai-chat
    options {
        model gpt-3.5-turbo
    }
}
```

</Step>
<Step title="Calling in application logic">
<Tabs>
<Tab title="Calling in python">
```python
# Import the generated client
from baml_client import baml as b
# Import the generated Sentiment enum
from baml_client.baml_types import Sentiment

async def pipeline(arg: str) -> str: # Call the get sentiment function
sentiment = await b.GetSentiment(arg)

    if sentiment == Sentiment.Positive:
        return "I'm glad you're having a good day!"
    elif sentiment == Sentiment.Negative:
        return "I'm sorry to hear that."
    else:
        return "I see its just a normal day for you."

````
</Tab>
<Tab title="Using a specific impl">
```python
# Use the specific version of the prompt we defined.
sentiment = await b.GetSentiment.get_impl(
    'my_version').run(arg)
````

</Tab>
</Tabs>
</Step>
</Steps>

## Type Safety (error handling)

Baml uses our `Forgiving Parser` to help extract data as much as possible.

## Injecting inputs into the prompt

### #input

`#input` allows you to pass in the function's input text (or object) to the LLM prompt

#### How #input behaves if it's a string or enum

Using \{#input\} in your prompt is the equivalent of `str(arg)`.

#### How #input behaves if it's a custom type

If you add **\{#input\}** directly to your prompt when the input is a custom `class`, we serialize the object to JSON into your prompt. To prevent this you can inject each field individually like `{#input.field}`.

### Formatting the #output

The #output is the function's return type.

`#print_type(output)` in the prompt is used to print out the JSON schema of the output type in your prompt.
Baml automatically generates a JSON schema for you, but you can customize them with schema attributes.

#### Add a custom output schema description

In LLMs it is often useful to rename or provide descriptions about the task you are attempting to do. For example, you may want to rename a property from `duration` to `duration_in_minutes` so the LLM can understand what value it should output better.

For example we may want to change the **print_type(output)** from

```
{
  "duration": "integer"
}
```

to

```json
{
  "duration_in_minutes": "The duration in minutes, as an integer"
}
```

without having to change your code or prompt.

To do this you can just add **@alias** and **@description** attributes to the `class` definition.

```diff
function GetDuration {
    input string
    output Duration
}

class Duration {
    duration int
+    @alias(duration_in_minutes)
+    @description("The duration in minutes, as an integer")
}

impl<llm, GetDuration> V1 {
    prompt #"
        Given a sentence, return the duration of the sentence.

        {#input}

        Duration:
        {#print_type(output)}

        Duration:
    "#
}


```

`{#print_type(output)}` will automatically parse all your field attributes and replace the default JSON schema with your own custom aliases and descriptions of each field.

#### print_enum

`{#print_enum(enum)}` is used to print out the values of an enum in your prompt.

To modify the description of each value, see the examples below.

<CodeGroup>
```llvm Ex 1: describe
enum Sentiment {
    Positive @description("When the sentence is about good things that happened recently")
    Negative @description("When the sentence is about bad things that happened recently")
    Neutral @description("Sentences that are neutral or happend too long ago to be relevant")
}

impl<llm, GetSentiment> V1 {
prompt #"
Given a sentence, return the sentiment of the sentence.

        {#input}

        Sentiments:
        {#print_enum(Sentiment)}

        Sentiment:
    "#

}

````

```llvm Ex 1: Prompt
Given a sentence, return the sentiment of the sentence.

{#input}

Sentiments:
Positive: When the sentence is about good things that happened recently
Negative: When the sentence is about bad things that happened recently
Neutral: Sentences that are neutral or happend too long ago to be relevant

Sentiment:
````

```llvm Ex 2: Rename
# In this example, we rename what we call each `Sentiment` value, but still
# guarantee the `Sentiment` enum as an output (thanks to Baml's parser).
class Sentiment {
    Positive @alias(Good) @description("When the sentence is about good things that happened recently")
    Negative @alias(Bad) @description("When the sentence is about bad things that happened recently")
    Neutral @description("Sentences that are neutral or happend too long ago to be relevant")
}

...

```

```llvm Ex 2: Prompt
Given a sentence, return the sentiment of the sentence.

{#input}

Sentiments:
Good: When the sentence is about good things that happened recently
Bad: When the sentence is about bad things that happened recently
Neutral: Sentences that are neutral or happend too long ago to be relevant

Sentiment:
```

```diff Ex 3: Skip
# You can also skip any values you don't want to parse.

class Sentiment {
    Positive @alias(Good) @description("When the sentence is about good things that happened recently")
    Negative @alias(Bad) @description("When the sentence is about bad things that happened recently")
+    Neutral @alias(Neither) @skip @description("Sentences that are neutral or happend too long ago to be relevant")
}

```

```llvm Ex 3: Prompt
Given a sentence, return the sentiment of the sentence.

{#input}

Sentiments:
Good: When the sentence is about good things that happened recently
Bad: When the sentence is about bad things that happened recently

Sentiment:
```

</CodeGroup>
