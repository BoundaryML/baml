---
title: Prompt Syntax
---

Prompts are written using the [Jinja templating language](https://jinja.palletsprojects.com/en/3.0.x/templates/).

There are **2 jinja macros** (or functions) that we have included into the language for you. We recommend viewing what they do using the VSCode preview (or in [promptfiddle.com](promptfiddle.com)), so you can see the full string transform in real time.

1. **`{{ _.role("user") }}`**: This divides up the string into different message roles.
2. **`{{ ctx.output_format }}`**: This prints out the output format instructions for the prompt.
You can add your own prefix instructions like this: `{{ ctx.output_format(prefix="Please please please format your output like this:")}}`. We have more parameters you can customize. Docs coming soon.
3. **`{{ ctx.client }}`**: This prints out the client model the function is using

"ctx" is contextual information about the prompt (like the output format or client). "_." is a special namespace for other BAML functions.



Here is what a prompt with jinja looks like using these macros:

```rust
enum Category {
    Refund
    CancelOrder
    TechnicalSupport
    AccountIssue
    Question
}

class Message {
  role string
  message string
}


function ClassifyConversation(messages: Message[]) -> Category[] {
  client GPT4Turbo
  prompt #"
    Classify this conversation:
    {% for m in messages %}
      {{ _.role(m.role) }}
      {{ m.message }}
    {% endfor %}

    Use the following categories:
    {{ ctx.output_format}}
  "#
}
```

### Template strings
You can create your own typed templates using the `template_string` keyword, and call them from a prompt:

```rust
// Extract the logic out of the prompt:
template_string PrintMessages(messages: Message[]) -> string {
  {% for m in messages %}
    {{ _.role(m.role) }}
    {{ m.message }}
  {% endfor %}
}

function ClassifyConversation(messages: Message[]) -> Category[] {
  client GPT4Turbo
  prompt #"
    Classify this conversation:
    {{ PrintMessages(messages) }}

    Use the following categories:
    {{ ctx.output_format}}
  "#
}
```

### Conditionals
You can use these special variables to write conditionals, like if you want to change your prompt depending on the model:
  
  ```rust
  {% if ctx.client.name == "GPT4Turbo" %}
    // Do something
  {% endif %}
  ```

You can use conditionals on your input objects as well:
  
  ```rust
  {% if messages[0].role == "user" %}
    // Do something
  {% endif %}
  ```
