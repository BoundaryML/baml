entry = {
    SOI ~ segment* ~ EOI
}

segment = {  code_block | comment_block | dangling_code_block | dangling_comment_block | prompt_text | whitespaces }
whitespaces = { WHITESPACE+ }
code_block    = { BLOCK_OPEN ~ WHITESPACE* ~ (print_block | chat_block | variable) ~ WHITESPACE* ~ BLOCK_CLOSE }
print_block   = { print_keyword ~ template_args? ~ arg_list? }
template_args = { "<" ~ identifier ~ ">" }
print_keyword = _{ "print" ~ identifier }

chat_block = { chat_keyword ~ template_args? ~ arg_list? }
chat_keyword = _{ "chat" }
arg_list = { "(" ~ WHITESPACE* ~ variable? ~ ("," ~ WHITESPACE* ~ variable)? ~ WHITESPACE* ~ ")" }
variable    = { identifier ~ ("." ~ identifier)* }

identifier = {(ASCII_ALPHA | "_")+}

comment_text_2 = { (!"///}" ~ ANY)* }
comment_text_1 = { (!"//}" ~ ANY)* }
comment        = {
    ("{///" ~ comment_text_2 ~ "///}")
  | ("{//" ~ comment_text_1 ~ "//}")
}

BLOCK_OPEN  = _{ "{#" }
BLOCK_CLOSE = _{ "}" }

dangling_code_block    = { BLOCK_OPEN ~ (!BLOCK_OPEN ~ !"{//" ~ !"{///" ~ !NEWLINE ~ ANY)+ }
dangling_comment_block = { ("{//" | "{///") ~ (!"//}" ~ !"///}" ~ ANY)+ }

comment_block = @{ comment }

WHITESPACE  = { " " | "\t" }
NEWLINE     = _{ "\n" | "\r\n" | "\r" }
// ignore matched whitespace with the @ since we do that separately to ensure we keep all proper format
prompt_text =  @{ (!("{#" | "{//" | "{///") ~ ANY)+ }
